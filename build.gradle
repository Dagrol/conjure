buildscript {
    repositories {
        maven {
            url 'https://artifactory.palantir.build/artifactory/all-jar/'
        }
    }

    dependencies {
        classpath 'gradle.plugin.com.palantir:gradle-circle-style:1.0.0'
        classpath 'com.netflix.nebula:nebula-dependency-recommender:5.0.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:0.17.0'
        classpath 'com.palantir.exartisi:gradle-exartisi-plugin:2.0.0'
        classpath 'com.palantir.configurationresolver:gradle-configuration-resolver-plugin:0.3.0'
        classpath 'com.palantir.internalpublish:gradle-internal-publish-plugin:0.2.0'
        classpath 'gradle.plugin.com.palantir.python:gradle-miniconda-plugin:0.8.0'
    }
}

plugins {
    id 'com.palantir.git-version' version '0.5.2'
    id 'org.inferred.processors' version '1.2.12'
}

apply plugin: 'java'
apply plugin: 'com.palantir.baseline-config'
apply plugin: 'com.palantir.baseline-idea'
apply plugin: 'com.palantir.circle.style'

allprojects {
    version gitVersion()
    group 'com.palantir.conjure'

    repositories {
        maven {
            url 'https://artifactory.palantir.build/artifactory/all-jar/'
        }
    }
    apply plugin: 'com.palantir.configuration-resolver'
    apply plugin: 'com.palantir.exartisi-dependencies'
    apply plugin: 'nebula.dependency-recommender'

    dependencyRecommendations {
        strategy OverrideTransitives
        propertiesFile file: project.rootProject.file('versions.props')
        def subprojectVersions = project.file('versions.props')
        if (subprojectVersions.exists()) {
            propertiesFile file: subprojectVersions
        }
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'com.palantir.baseline-checkstyle'
    apply plugin: 'com.palantir.baseline-eclipse'
    apply plugin: 'com.palantir.baseline-idea'
    apply plugin: 'com.palantir.exartisi-deprecation'
    apply plugin: 'com.palantir.baseline-error-prone'
    apply plugin: 'org.inferred.processors'
    apply plugin: 'maven-publish'

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    tasks.jacocoTestReport.dependsOn(tasks.test)
    tasks.check.dependsOn(tasks.jacocoTestReport)
    tasks.check.dependsOn(javadoc)

    sourceCompatibility = 1.8

    if (System.env.CIRCLE_TEST_REPORTS) {
        test.reports.junitXml.destination = new File(System.env.CIRCLE_TEST_REPORTS, it.getName())
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-XepDisableWarningsInGeneratedCode']
    }

    configurations.errorprone {
        resolutionStrategy {
            force 'com.google.guava:guava:21.0'
        }
    }
}

dependencies {
    baseline 'com.palantir.baseline:gradle-baseline-java-config:0.17.0@zip'
}

