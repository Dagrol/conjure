apply from: "${rootDir}/gradle/java.gradle"
apply plugin: 'com.palantir.java-distribution'
apply plugin: 'com.palantir.publish-dist'

jacocoCoverage {
    reportThreshold 0.5
}

distribution {
    serviceName 'typescript-ete-test'
    mainClass 'com.palantir.typescriptetetest.TypescriptEteTestApplication'
    args 'server', 'var/conf/server.yml'
}

launchConfig {
      excludedTasks 'run'
}

task runDev(type: JavaExec) {
    dependsOn ':typescript-ete-test-app:buildDev'
    classpath project.sourceSets.main.runtimeClasspath
    main 'com.palantir.typescriptetetest.TypescriptEteTestApplication'
    args('server', 'dev/conf/server.yml')
}

def generatedResources = "${buildDir}/web-resources"
sourceSets {
    main {
        output.dir(generatedResources)
    }
}

task('syncWebAssets', type: Sync) {
    dependsOn ':typescript-ete-test-app:build'
    from '../typescript-ete-test-app/build/min'
    destinationDir file("${generatedResources}/assets")
}

tasks.jar.dependsOn 'syncWebAssets'
tasks.run.dependsOn 'syncWebAssets'

dependencies {
    compile project(':typescript-ete-test-api')

    compile "com.palantir.config.crypto:encrypted-config-value-bundle:${encryptedConfigVersion}"
    compile "com.palantir.indexpage:dropwizard-index-page:${indexPageVersion}"
    compile "com.palantir.multipass:dropwizard-multipass:${multipassVersion}"
    compile "com.palantir.remoting:http-servers:${httpRemotingVersion}"
    compile "com.palantir.websecurity:dropwizard-web-security:${webSecurityVersion}"
    compile "io.dropwizard:dropwizard-core:${dropwizardVersion}"
    compile "io.dropwizard-bundles:dropwizard-configurable-assets-bundle:${configurableAssetsBundleVersion}"

    testCompile "com.palantir.remoting:http-clients:${httpRemotingVersion}"
    testCompile "com.palantir.remoting:ssl-config:${httpRemotingVersion}"
    testCompile "io.dropwizard:dropwizard-testing:${dropwizardVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
}

publishing {
    publications {
        dist(MavenPublication) {
            artifact distTar
        }
    }
}
